plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.25'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.9.25'
    id 'maven-publish'
    id 'org.jetbrains.kotlinx.kover' version '0.9.2'
    id 'org.jlleitschuh.gradle.ktlint' version '13.0.0'
}

group = 'com.ingsis.grupo10'
version = '0.0.1-SNAPSHOT'
description = 'Snippet Service'


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

def env = new Properties()
def envFile = file('.env')
if (envFile.isFile() && envFile.canRead()) {
    envFile.withInputStream { env.load(it) }
}


repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/austral-ingsis/class-redis-streams")
        credentials {
            username = System.getenv("GITHUB_ACTOR") ?: env.getProperty("GITHUB_ACTOR") ?: ""
            password = System.getenv("GITHUB_TOKEN") ?: env.getProperty("GITHUB_TOKEN") ?: ""
        }
    }
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation("org.austral.ingsis:redis-streams-mvc:0.1.13")
    implementation("org.springframework.boot:spring-boot-starter-data-redis")
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.1.0'
    testImplementation 'io.mockk:mockk:1.14.6'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict'
    }
}

allOpen {
    annotation 'jakarta.persistence.Entity'
    annotation 'jakarta.persistence.MappedSuperclass'
    annotation 'jakarta.persistence.Embeddable'
}


tasks.register("installGitHooks", Exec) {
    group = "setup"
    description = "Install git hooks"
    commandLine "git", "config", "core.hooksPath", ".github/hooks"
    doLast {
        println("Hooks configured via core.hooksPath")
    }
}

tasks.named('test') {
    useJUnitPlatform()
}


publishing {
    publications {
        create('mavenJava', MavenPublication) {
            from(components.java)
            groupId = project.group
            artifactId = "${project.name}-service"
            version = project.version
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/IngSisG10/printscript-service")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: env.getProperty("GITHUB_ACTOR") ?: ""
                password = System.getenv("GITHUB_TOKEN") ?: env.getProperty("GITHUB_TOKEN") ?: ""
            }
        }
    }
}